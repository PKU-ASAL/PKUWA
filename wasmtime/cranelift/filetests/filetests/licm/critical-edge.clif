test licm
target riscv32

; The loop in this function is entered from a critical edge.

function %critical_edge(i32, i32) -> i32 {

            block0(v0: i32, v7: i32):
[SBzero#38]   brnz v7, block2(v0)
[UJ#1b]       jump block1

            block1:
[Iret#19]     return v0

            block2(v1: i32):
              v2 = iconst.i32 1
              v3 = iconst.i32 2
              v4 = iadd v2, v3
[SBzero#18]   brz v1, block4(v1)
[UJ#1b]       jump block3

            block3:
              v5 = isub v1, v2
[UJ#1b]       jump block2(v5)

            block4(v6: i32):
[Iret#19]     return v6

}
; sameln: function %critical_edge
; nextln: block0(v0: i32, v7: i32):
; nextln:     brnz v7, block5(v0)
; nextln:     jump block1
; nextln: 
; nextln: block1:
; nextln:     return v0
; nextln: 
; nextln: block5(v8: i32):
; nextln:     v2 = iconst.i32 1
; nextln:     v3 = iconst.i32 2
; nextln:     v4 = iadd v2, v3
; nextln:     jump block2(v8)
; nextln: 
; nextln: block2(v1: i32):
; nextln:     brz v1, block4(v1)
; nextln:     jump block3
; nextln: 
; nextln: block3:
; nextln:     v5 = isub.i32 v1, v2
; nextln:     jump block2(v5)
; nextln: 
; nextln: block4(v6: i32):
; nextln:     return v6
; nextln: }
